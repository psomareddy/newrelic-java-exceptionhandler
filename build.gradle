
plugins {
    id 'java-library'
	id 'eclipse'
}

group = 'com.newrelic.labs.instrumentation'
version = '1.0.0'
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
   maven {
      url 'https://repo1.maven.org/maven2'
   }
}

dependencies {
    testImplementation 'junit:junit:4.13'
	implementation group: 'com.newrelic.agent.java', name: 'newrelic-api', version: '6.5.0'
	implementation group: 'com.newrelic.agent.java', name: 'newrelic-agent', version: '6.5.0'

}

jar {
  manifest {
       attributes 'Implementation-Title': project.group + '.' + project.name, 
				  'Implementation-Vendor-Id': 'Field Instrumentation', 
				  'Implementation-Version': version
  }
}

task install(dependsOn: jar, type: Copy) {
    description = 'Copies compiled jar to the NEW_RELIC_EXTENSIONS_DIR.'
    group  = 'New Relic'

    def extDir = System.getenv("NEW_RELIC_EXTENSIONS_DIR") ?: " "

    from jar
    into extDir
}

install.doFirst  {
    def extDir = System.getenv("NEW_RELIC_EXTENSIONS_DIR")
     if (extDir == null) {
         throw new Exception("Must set NEW_RELIC_EXTENSIONS_DIR.")
     }

     if (extDir.startsWith("~" + File.separator)) {
         extDir = System.getProperty("user.home") + extDir.substring(1);
     }

     if (!file(extDir).directory) {
         throw new Exception(extDir + "NEW_RELIC_EXTENSIONS_DIR, set as '" + extDir + "'is not a valid directory.")
     }
}
